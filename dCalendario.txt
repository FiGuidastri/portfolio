let

//Alterar somente a Tabela e a coluna de datas
/* A Tabela é onde possui uma coluna Data que deseja usar em seus cálculos ou visuais. 
Exemplo. Tabela fVendas que possui a coluna Data Pedido Ficaria List.Min (fVendas[Data Pedido]).
Se Possuir mais de uma coluna ficaria List.Min (fVendas[Data Pedido] & fVendas[Data Vencimento]).
*/

    MenorData = List.Min (fLancamentos[competencia_data]),
    MaiorData = List.Max (fLancamentos[competencia_data]),
	
//Não alterar daqui pra baixo	
	
    DataInicio = Date.StartOfYear(MenorData),
    DataFim = Date.EndOfYear(MaiorData),
    Dias = Duration.Days(DataFim- DataInicio) +1,
    ListarDatas = List.Dates(DataInicio, Dias, #duration(1,0,0,0)),
    Tabela = #table(
        type table[
            Data = date,
            Ano = Int64.Type,
            NomeMes = text,
            MesAbre = text,
            MesAno = text,
            MesNum = number,
            AnoMesINT = number,
            InicioMes = date,
            Trimestre = Int64.Type,
            TrimestreAbreviado = text,
            Bimestre = text,
            Semestre = text,
            Semana = Int64.Type,
            DiaSemana = Int64.Type,
            NomeDia = text 
        ],
        List.Transform(
            ListarDatas,
            each {
                _,
                Date.Year(_),
                Text.Proper( Date.MonthName(_)),
                Text.Proper(Text.Start(Date.MonthName(_), 3)),
                Text.Proper(Text.Start(Date.MonthName(_), 3)) & "-" & Text.End(Text.From(Date.Year(_)), 2),
                Date.Month(_),
                Date.Year(_) * 100 + Date.Month(_),
                Date.StartOfMonth(_),
                Date.QuarterOfYear(_),
                Text.From(Date.QuarterOfYear(_)) & "º Trim ",
                Text.From( Number.RoundUp( Date.Month(_)/2,0)) & "º Bim",
                Text.From( Number.RoundUp( Date.Month(_)/6,0)) & "º Sem",
                Date.WeekOfMonth(_),
                Date.DayOfWeek(_),
                Date.DayOfWeekName(_)

            }

        )
    )
in
    Tabela